UP1 = SL & ".." & SL
UP2 = SL & ".." & UP1
UP3 = SL & ".." & UP2

% Determine which compiler variant we have.
if defined("INITIAL_REACTOR_EDITOR")
  M3_VARIANT = "CM3"
else
  if not defined("build_standalone")
    M3_VARIANT = "PM3"
  else
    M3_VARIANT = "SRC"
  end
end

if not defined("PM3")
  % The procedure "build_standalone" exists only in SRC Modula-3.
  if not defined("build_standalone")
    PM3 = "T"
  end
end
if defined("PM3")
  proc build_standalone() is
    option("standalone", "T")
  end
end
if defined("STATIC")
  build_standalone()
end

% Generate the "Creation.i3/m3" interface and implementation file. 
% It contains the creation date of the module.
proc creation_date_module(name) is
  local filename = name & ".m3"
  local tempname = "." & filename
  local cmd = ""
  local ret = 0
  > tempname in
    write("MODULE Creation;", CR, CR, "BEGIN", CR, "  Date   := ")
    write("\"", datetime(), "\";", CR)
  end
  >> tempname in
    if equal (OS_TYPE, "WIN32")
      %write("  System := ")
      write("  System := \"")
    else
      write("  System := \"")
    end
  end
  if equal (OS_TYPE, "WIN32")
    %cmd = "act_uname_string"
    cmd = "c:\\cygwin\\bin\\bash -c 'echo `uname -msr`\\\"\\; >>'" & tempname
  else
    cmd = "echo `uname -msr`'\";'"
  end
  cmd = cmd & " >> " & tempname
  if defined("PM3")
    ret = exec(["/bin/sh", "-c", cmd])
  else
    %if equal (OS_TYPE, "WIN32")
    %  cmd = "bash -c " & cmd
    %end
    exec("@" & cmd)
  end
  >> tempname in
    write("END Creation.", CR)
  end
  cp_if(tempname, filename)
  derived_implementation(name)
  filename = name & ".i3"
  tempname = "." & filename
  cmd = ""
  > tempname in
    write("INTERFACE Creation;", CR, CR, "VAR Date, System : TEXT; ", CR)
  end
  >> tempname in
    write("END Creation.", CR)
  end
  cp_if(tempname, filename)
  derived_interface(name, "hidden")
end

% Generate the "Version.m3" implementation file.  It contains the release
% name, if any, else the compilation date and time.
proc version_impl(name) is
  local filename = name & ".m3"
  local tempname = "." & filename
  local cmd = ""
  local ret = 0
  if defined("_all")
    > tempname in
      write("MODULE ", name, ";", CR, CR, "BEGIN", CR, "  Name := ")
    end
    vincl = "COMPACT_VERSION"
    if stale(vincl, vincl)
      COMPACT_VERSION_TEXT = "CM3 ComPact 1.0"
    else
      include(vincl)
    end
    if equal("$Name: not supported by cvs2svn $", "$" & "Name:  $")
      cmd = "date -u +'\"U_%Y_%m_%d_%H_%M_%S' | tr '\n' ' '"
    else
      cmd = "echo '$Name: not supported by cvs2svn $' | awk '{print \"\\\"\" $2 \"\";}'"
    end
    cmd = cmd & " >> " & tempname
    if equal(M3_VARIANT, "PM3")
      ret = exec(["/bin/sh", "-c", cmd])
    else
      exec("@" & cmd)
    end
    >> tempname in
      write(COMPACT_VERSION_TEXT & "\";", CR)
      write("  Target := \"", TARGET, "\";", CR)
    end
    >> tempname in
      write("END ", name, ".", CR)
    end
    cp_if(tempname, filename)
    derived_implementation(name)
  end
end

