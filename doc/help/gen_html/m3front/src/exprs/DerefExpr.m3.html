<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3front/src/exprs/DerefExpr.m3</TITLE>
</HEAD>
<BODY bgcolor="#eeeeee">
<A NAME="0TOP0">
<H2>m3front/src/exprs/DerefExpr.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              

<P> File: DerefExpr.m3                                          
 Last modified on Fri Feb 24 07:43:48 PST 1995 by kalsow     
      modified on Thu Nov 29 06:04:10 1990 by muller         

<P><PRE>MODULE <module><implements><A HREF="DerefExpr.i3.html">DerefExpr</A></implements></module>;

IMPORT <A HREF="Expr.i3.html">Expr</A>, <A HREF="ExprRep.i3.html">ExprRep</A>, <A HREF="../types/RefType.i3.html">RefType</A>, <A HREF="../misc/Error.i3.html">Error</A>, <A HREF="#x1">Type</A>;
IMPORT <A HREF="NilChkExpr.i3.html">NilChkExpr</A>, <A HREF="../misc/CG.i3.html">CG</A>, <A HREF="../builtinTypes/ErrType.i3.html">ErrType</A>;

TYPE
  P = ExprRep.Ta BRANDED &quot;DerefExpr.P&quot; OBJECT
      OVERRIDES
        typeOf       := TypeOf;
        check        := Check;
        need_addr    := NeedsAddress;
        prep         := Prep;
        compile      := Compile;
        prepLV       := Prep;
        compileLV    := CompileLV;
        prepBR       := ExprRep.PrepNoBranch;
        compileBR    := ExprRep.NoBranch;
        evaluate     := ExprRep.NoValue;
        isEqual      := ExprRep.EqCheckA;
        getBounds    := ExprRep.NoBounds;
        isWritable   := ExprRep.IsAlways;
        isDesignator := ExprRep.IsAlways;
	isZeroes     := ExprRep.IsNever;
	genFPLiteral := ExprRep.NoFPLiteral;
	prepLiteral  := ExprRep.NoPrepLiteral;
	genLiteral   := ExprRep.NoLiteral;
        note_write   := NoteWrites;
      END;

PROCEDURE <A NAME="New"><procedure>New</procedure></A> (a: Expr.T): Expr.T =
  VAR p: P;
  BEGIN
    p := NEW (P);
    ExprRep.Init (p);
    p.a := NilChkExpr.New (a);
    p.origin := p.a.origin;
    RETURN p;
  END New;

PROCEDURE <A NAME="SetOffset"><procedure>SetOffset</procedure></A> (e: Expr.T; n: INTEGER) =
  BEGIN
    TYPECASE e OF
    | NULL =&gt; (* nothing *)
    | P(p) =&gt; NilChkExpr.SetOffset (p.a, n);
    ELSE      (* nothing *)
    END;
  END SetOffset;

PROCEDURE <A NAME="TypeOf"><procedure>TypeOf</procedure></A> (p: P): Type.T =
  VAR ta, target: Type.T;
  BEGIN
    ta := Expr.TypeOf (p.a);
    IF RefType.Split (ta, target)
      THEN RETURN target;
      ELSE RETURN ErrType.T;
    END;
  END TypeOf;

PROCEDURE <A NAME="Check"><procedure>Check</procedure></A> (p: P;  VAR cs: Expr.CheckState) =
  VAR tx, ta, target: Type.T;  err0, err1, warn: INTEGER;
  BEGIN
    Error.Count (err0, warn);
    Expr.TypeCheck (p.a, cs);
    tx := Expr.TypeOf (p.a);
    Error.Count (err1, warn);
    ta := Type.Base (tx);
    target := NIL;
    IF ((tx = NIL) OR (tx = ErrType.T)) AND (err0 # err1) THEN
      (* already an error, don't generate any more *)
      target := ErrType.T;
    ELSIF NOT RefType.Split (ta, target) THEN
      Error.Msg (&quot;cannot dereference a non-REF value&quot;);
      target := ErrType.T;
    ELSIF (target = NIL) THEN
      Error.Msg (&quot;cannot dereference REFANY, ADDRESS, or NULL&quot;);
      target := ErrType.T;
    END;
    p.type := target;
  END Check;

PROCEDURE <A NAME="NeedsAddress"><procedure>NeedsAddress</procedure></A> (&lt;*UNUSED*&gt; p: P) =
  BEGIN
    (* ok *)
  END NeedsAddress;

PROCEDURE <A NAME="Prep"><procedure>Prep</procedure></A> (p: P) =
  BEGIN
    Expr.Prep (p.a);
  END Prep;

PROCEDURE <A NAME="Compile"><procedure>Compile</procedure></A> (p: P) =
  VAR t := p.type;  info: Type.Info;
  BEGIN
    Expr.Compile (p.a);
    EVAL Type.CheckInfo (t, info);
    CG.Force ();  (*'cause alignment applies to the referent, not the pointer*)
    CG.Boost_alignment (info.alignment);
    Type.LoadScalar (t);
  END Compile;

PROCEDURE <A NAME="CompileLV"><procedure>CompileLV</procedure></A> (p: P) =
  VAR info: Type.Info;
  BEGIN
    Expr.Compile (p.a);
    EVAL Type.CheckInfo (p.type, info);
    CG.Force ();  (*'cause alignment applies to the referent, not the pointer*)
    CG.Boost_alignment (info.alignment);
  END CompileLV;

PROCEDURE <A NAME="NoteWrites"><procedure>NoteWrites</procedure></A> (p: P) =
  BEGIN
    Expr.NoteWrite (p.a);
  END NoteWrites;

BEGIN
END DerefExpr.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Type is in:
</A><UL>
<LI><A HREF="../types/Type.i3.html#0TOP0">m3front/src/types/Type.i3</A>
<LI><A HREF="../../../sharedobjgen/src/Type.i3.html#0TOP0">sharedobjgen/src/Type.i3</A>
<LI><A HREF="../../../stablegen/src/Type.i3.html#0TOP0">stablegen/src/Type.i3</A>
<LI><A HREF="../../../stubgen/src/Type.i3.html#0TOP0">stubgen/src/Type.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
