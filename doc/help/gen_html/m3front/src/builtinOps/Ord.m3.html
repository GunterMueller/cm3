<HTML>
<HEAD>
<TITLE>Critical Mass Modula-3: m3front/src/builtinOps/Ord.m3</TITLE>
</HEAD>
<BODY bgcolor="#ffffff">
<A NAME="0TOP0">
<H2>m3front/src/builtinOps/Ord.m3</H2></A><HR>
<inModule>
<PRE></PRE> Copyright (C) 1992, Digital Equipment Corporation           
 All rights reserved.                                        
 See the file COPYRIGHT for a full description.              

<P> File: Ord.m3                                                
 Last Modified On Tue May  3 16:32:32 PDT 1994 By kalsow     
      Modified On Sat Dec  8 00:54:19 1990 By muller         

<P><PRE>MODULE <module><implements><A HREF="Ord.i3.html">Ord</A></implements></module>;

IMPORT <A HREF="../exprs/CallExpr.i3.html">CallExpr</A>, <A HREF="../exprs/Expr.i3.html">Expr</A>, <A HREF="../exprs/ExprRep.i3.html">ExprRep</A>, <A HREF="#x1">Type</A>, <A HREF="../values/Procedure.i3.html">Procedure</A>, <A HREF="../builtinTypes/Int.i3.html">Int</A>, <A HREF="../builtinTypes/LInt.i3.html">LInt</A>, <A HREF="../misc/Error.i3.html">Error</A>;
IMPORT <A HREF="../exprs/IntegerExpr.i3.html">IntegerExpr</A>, <A HREF="../exprs/EnumExpr.i3.html">EnumExpr</A>, <A HREF="../exprs/CheckExpr.i3.html">CheckExpr</A>, <A HREF="../../../m3middle/src/Target.i3.html">Target</A>, <A HREF="../../../m3middle/src/TInt.i3.html">TInt</A>, <A HREF="../misc/CG.i3.html">CG</A>;

VAR Z: CallExpr.MethodList;

PROCEDURE <A NAME="Check"><procedure>Check</procedure></A> (ce: CallExpr.T;  VAR cs: Expr.CheckState) =
  VAR e := ce.args[0];  t := Expr.TypeOf (e);
      emin, emax: Target.Int;
  BEGIN
    IF NOT Type.IsOrdinal (t) THEN
      Error.Msg (&quot;ORD: argument must be an ordinal&quot;);
    END;
    ce.type := Int.T;
    IF Type.IsSubtype (t, LInt.T) AND ce.type = Int.T THEN
      (* must bound check the result *)
      Expr.GetBounds (e, emin, emax);
      IF TInt.LT (emin, Target.Integer.min) THEN
        (* we need a lower bound check *)
        IF TInt.LT (Target.Integer.max, emax) THEN
          (* we also need an upper bound check *)
          e := CheckExpr.New (e, Target.Integer.min, Target.Integer.max,
                              CG.RuntimeError.ValueOutOfRange);
          Expr.TypeCheck (e, cs);
          ce.args[0] := e;
        ELSE
          e := CheckExpr.NewLower (e, Target.Integer.min,
                                   CG.RuntimeError.ValueOutOfRange);
          Expr.TypeCheck (e, cs);
          ce.args[0] := e;
        END;
      ELSIF TInt.LT (Target.Integer.max, emax) THEN
        (* we need an upper bound check *)
        e := CheckExpr.NewUpper (e, Target.Integer.max,
                                 CG.RuntimeError.ValueOutOfRange);
        Expr.TypeCheck (e, cs);
        ce.args[0] := e;
      END;
    END;
  END Check;

PROCEDURE <A NAME="Compile"><procedure>Compile</procedure></A> (ce: CallExpr.T) =
  VAR e := ce.args[0];  t := Expr.TypeOf (e);
  BEGIN
    Expr.Compile (e);
    IF Type.IsSubtype (t, LInt.T) THEN
      CG.Loophole (Target.Longint.cg_type, Target.Integer.cg_type);
    END;
  END Compile;

PROCEDURE <A NAME="Fold"><procedure>Fold</procedure></A> (ce: CallExpr.T): Expr.T =
  VAR e: Expr.T;  i: Target.Int;  t: Type.T;
  BEGIN
    e := Expr.ConstValue (ce.args[0]);
    IF (e = NIL) THEN
      RETURN NIL;
    ELSIF EnumExpr.Split (e, i, t) THEN
      RETURN IntegerExpr.New (Int.T, i);
    ELSIF IntegerExpr.Split (e, i, t) THEN
      RETURN IntegerExpr.New (Int.T, i);
    ELSE
      RETURN NIL;
    END;
  END Fold;

PROCEDURE <A NAME="GetBounds"><procedure>GetBounds</procedure></A> (ce: CallExpr.T;  VAR min, max: Target.Int) =
  VAR e := ce.args[0];
  BEGIN
    Expr.GetBounds (e, min, max);
    IF TInt.LT (min, Target.Integer.min) THEN min := Target.Integer.min END;
    IF TInt.LT (Target.Integer.max, max) THEN max := Target.Integer.max END;
  END GetBounds;

PROCEDURE <A NAME="Initialize"><procedure>Initialize</procedure></A> () =
  BEGIN
    Z := CallExpr.NewMethodList (1, 1, TRUE, FALSE, TRUE, Int.T,
                                 NIL,
                                 CallExpr.NotAddressable,
                                 Check,
                                 CallExpr.PrepArgs,
                                 Compile,
                                 CallExpr.NoLValue,
                                 CallExpr.NoLValue,
                                 CallExpr.NotBoolean,
                                 CallExpr.NotBoolean,
                                 Fold,
                                 GetBounds,
                                 CallExpr.IsNever, (* writable *)
                                 CallExpr.IsNever, (* designator *)
                                 CallExpr.NotWritable (* noteWriter *));
    Procedure.Define (&quot;ORD&quot;, Z, TRUE);
  END Initialize;

BEGIN
END Ord.
</PRE>
</inModule>
<HR>
<A NAME="x1">interface Type is in:
</A><UL>
<LI><A HREF="../../../cm3ide/src/nodes/Type.i3.html#0TOP0">cm3ide/src/nodes/Type.i3</A>
<LI><A HREF="../types/Type.i3.html#0TOP0">m3front/src/types/Type.i3</A>
<LI><A HREF="../../../sharedobjgen/src/Type.i3.html#0TOP0">sharedobjgen/src/Type.i3</A>
<LI><A HREF="../../../stablegen/src/Type.i3.html#0TOP0">stablegen/src/Type.i3</A>
<LI><A HREF="../../../stubgen/src/Type.i3.html#0TOP0">stubgen/src/Type.i3</A>
</UL>
<P>
<PRE>























</PRE>
</BODY>
</HTML>
