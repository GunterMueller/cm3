%
%  Quake functions to instantiate operations on generic matrices.
%

readonly proc MatrixBasic (nm, rfield, vfield) is
  local basic = nm & "MatrixBasic"
  build_generic_intf (basic, "MatrixBasic", [rfield, vfield], VISIBLE)
  build_generic_impl (basic, "MatrixBasic", [rfield, vfield])
end

readonly proc MatrixFast (nm, rfield, vfield) is
  local basic = nm & "MatrixBasic"
  local fast  = nm & "MatrixFast"
  build_generic_intf (basic, "MatrixBasic", [rfield, vfield], VISIBLE)
  build_generic_impl (basic, "MatrixBasic", [rfield, vfield])
  build_generic_intf (fast,  "MatrixFast",  [rfield, vfield, basic], VISIBLE)
  build_generic_impl (fast,  "MatrixFast",  [rfield, vfield])
end

readonly proc MatrixTrans (nm, impl, real, complex, cimpl) is
  local basic = nm & "MatrixTrans"
  local rfield = real & "Basic"
  local rtrans = real & "Trans"
  local ctrans = real & complex & "Trans"
  local eigen  = nm & "EigenSystem"
  local mfield = nm & "Matrix" & impl
  build_generic_intf (basic, "MatrixTrans", [rfield,                        mfield], VISIBLE)
  build_generic_impl (basic, "MatrixTrans", [rfield, rtrans, ctrans, eigen, mfield])
end

readonly proc MatrixDecomposition (nm, impl) is
  local basic = nm & "MatrixDecomposition"
  local rfield  = nm & "Basic"
  local rtrans  = nm & "Trans"
  local vfield  = nm & "Vector" & impl
  local mfield  = nm & "Matrix" & impl
  build_generic_intf (basic, "MatrixDecomposition", [rfield,         vfield, mfield], VISIBLE)
  build_generic_impl (basic, "MatrixDecomposition", [rfield, rtrans, vfield, mfield])
end

readonly proc EigenSystem (nm, impl) is
  local basic = nm & "EigenSystem"
  local rfield  = nm & "Basic"
  local rtrans  = nm & "Trans"
  local vfield  = nm & "Vector" & impl
  local mfield  = nm & "Matrix" & impl
  build_generic_intf (basic, "EigenSystem", [        rtrans, vfield, mfield], VISIBLE)
  build_generic_impl (basic, "EigenSystem", [rfield, rtrans, vfield, mfield])
end

readonly proc CharPolynomial (nm, mimpl, rimpl) is
  local basic   = nm & "CharPolynomial"
  local rfield  = nm & rimpl
  local rtfield = nm & "RootBasic"
  local mfield  = nm & "Matrix" & mimpl
  build_generic_intf (basic, "CharPolynomial", [        rtfield, mfield], VISIBLE)
  build_generic_impl (basic, "CharPolynomial", [rfield, rtfield, mfield])
end

readonly proc MatrixFmtLex (nm, mimpl) is
  local mfield = nm & "Matrix" & mimpl
  local mfmt   = nm & "MatrixFmtLex"
  local rfmt   = nm & "FmtLex"
  build_generic_intf (mfmt, "MatrixFmtLex", [rfmt, mfield], VISIBLE)
  build_generic_impl (mfmt, "MatrixFmtLex", [rfmt])
end
